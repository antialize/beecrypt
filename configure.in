dnl Process this file with autoconf to produce a configure script
AC_INIT()
AC_CANONICAL_SYSTEM

dnl Check system type
case $target_os in
  freebsd*)
    AC_DEFINE(FREEBSD)
    ;;
  linux*)
    AC_DEFINE(LINUX)
    ;;
  openbsd*)
    AC_DEFINE(OPENBSD)
    ;;
  solaris*)
    AC_DEFINE(SOLARIS)
    ;;
  cygwin*)
	AC_DEFINE(WIN32)
	;;
  *)
    AC_MSG_WARN([Operating system type $target_os currently not supported and/or tested])
    ;;
esac

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL

AC_C_INLINE

if test "$ac_cv_prog_gcc" != yes; then
  AC_MSG_CHECKING([whether we are using Sun Workshop C])
  AC_EGREP_CPP(yes,
[ #ifdef __SUNPRO_C
  yes;
#endif
], ac_cv_prog_SUNPRO_CC=yes, ac_cv_prog_SUNPRO_CC=no)
  AC_MSG_RESULT($ac_cv_prog_SUNPRO_CC)
fi

dnl Check if we have the necessary java components installed
AC_ARG_ENABLE(javaglue, [  --enable-javaglue	creates the java glue code [default=yes]],
  [ if test "$enableval" = no; then
      ac_enable_javaglue=no
    else
      ac_enable_javaglue=yes
    fi
  ], [ ac_enable_javaglue=yes ])

if test "$ac_enable_javaglue" = yes ; then
  AC_CHECK_PROG(ac_cv_have_java, java, yes, no)
  AC_CHECK_PROG(ac_cv_have_javac, javac, yes, no)
  if test "$ac_cv_have_java" = yes; then
    if test "$ac_cv_have_javac" = yes; then
      AC_MSG_CHECKING([for Java JNI headers])
      cat > conftest.java << EOF
public class conftest
{
  public static void main(String[[]] argv)
  {
     System.out.println(System.getProperty("java.home"));
  }
}
EOF
      ac_cv_java_home=`javac conftest.java; java conftest`
      ac_cv_java_include="$ac_cv_java_home"/include
      if test -d "$ac_java_cv_include" ; then
        AC_MSG_RESULT($ac_cv_java_headers)
        ac_cv_java_headers=yes
      else
        ac_cv_java_include="$ac_cv_java_home"/../include
        if test -d "$ac_cv_java_include" ; then
          AC_MSG_RESULT($ac_cv_java_headers)
          ac_cv_java_headers=yes
        else
          AC_MSG_WARN([java headers not found, disabling javaglue])
          ac_cv_java_headers=no
          ac_enable_javaglue=no
        fi
      fi
    else
      AC_MSG_WARN([javac not found, disabling javaglue])
      ac_cv_java_headers=no
      ac_enable_javaglue=no
    fi
  else
    AC_MSG_WARN([java not found, disabling javaglue])
    ac_cv_java_headers=no
    ac_enable_javaglue=no
  fi
fi

if test "$ac_enable_javaglue" = yes ; then
  AC_DEFINE(JAVAGLUE,1)
  JAVAGLUE="javaglue.o"
  JNIFLAGS="-I$ac_cv_java_include"
  case $target_os in
  linux*)
    JNIFLAGS="$JNIFLAGS -I$ac_cv_java_include"/linux
    ;;
  solaris*)
    JNIFLAGS="$JNIFLAGS -I$ac_cv_java_include"/solaris
    ;;
  *)
    AC_MSG_WARN([please add approriate -I$ac_cv_java_include/<operating system> directory to JNIFLAGS])
    ;;
  esac
else
  AC_DEFINE(JAVAGLUE,0)
  JAVAGLUE=""
  JNIFLAGS=""
fi

AC_ARG_ENABLE(debug, [  --enable-debug	creates debugging code [default=no]],
  [ if test "$enableval" = no; then
      ac_enable_debug=no
    else
      ac_enable_debug=yes
    fi
  ], [ ac_enable_debug=no])

dnl Product and version
PRODUCT=beecrypt
VERSION=1.1.0
AC_DEFINE_UNQUOTED(PRODUCT, "$PRODUCT")
AC_DEFINE_UNQUOTED(VERSION, "$VERSION")
AC_DEFINE(JNIFLAGS, $JNIFLAGS)
AC_SUBST(PRODUCT)
AC_SUBST(VERSION)
AC_SUBST(CC)
AC_SUBST(CFLAGS)
AC_SUBST(LDFLAGS)
AC_SUBST(ASFLAGS)
AC_SUBST(JAVAGLUE)
AC_SUBST(JNIFLAGS)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T

dnl Checks for library functions.
AC_FUNC_MEMCMP

dnl Checks for header files.
AC_HEADER_STDC

AC_CHECK_HEADERS(errno.h string.h ctype.h stdlib.h alloca.h)
AC_CHECK_HEADERS(unistd.h fcntl.h limits.h)
AC_CHECK_HEADERS(time.h)
AC_CHECK_HEADERS(sys/types.h sys/stat.h sys/time.h)
AC_CHECK_HEADERS(thread.h pthread.h synch.h semaphore.h)
AC_CHECK_HEADERS(termio.h termios.h)
AC_CHECK_HEADERS(sys/audioio.h sys/ioctl.h sys/soundcard.h)

if test "$ac_cv_header_alloca_h" = yes; then
  AC_DEFINE(HAVE_ALLOCA, 1)
fi

if test "$ac_cv_header_sys_time_h" = yes; then
  AC_CHECK_FUNC(gettimeofday, [
    AC_DEFINE(HAVE_GETTIMEOFDAY, 1)
    ])
  AC_CHECK_FUNC(gethrtime, [
    AC_DEFINE(HAVE_GETHRTIME, 1)
    ])
fi

if test "$ac_cv_header_thread_h" = yes; then
  AC_HAVE_LIBRARY(thread)
elif test  "$ac_cv_header_pthread_h" = yes; then
  AC_HAVE_LIBRARY(pthread)
fi

dnl Checks for word sizes
AC_CHECK_SIZEOF(char)
AC_CHECK_SIZEOF(unsigned char)
AC_CHECK_SIZEOF(short)
AC_CHECK_SIZEOF(unsigned short)
AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(unsigned int)
AC_CHECK_SIZEOF(long)
AC_CHECK_SIZEOF(unsigned long)
AC_CHECK_SIZEOF(long long)
AC_CHECK_SIZEOF(unsigned long long)
AC_CHECK_SIZEOF(float)
AC_CHECK_SIZEOF(double)

dnl Checks for endianness
AC_C_BIGENDIAN

dnl Checks for compiler flags
if test "$ac_enable_debug" = yes; then
  if test "$ac_cv_prog_gcc" = yes; then
    CFLAGS="-g -shared"
  elif test "$ac_cv_prog_SUNPRO_CC" = yes; then
    CFLAGS="-g -mt -KPIC"
  else
    CFLAGS="-g -shared"
  fi
else
  if test "$ac_cv_prog_gcc" = yes; then
    CFLAGS="-O3 -shared"
    case $target_cpu in
    i386)
      ;;
    i486)
      CFLAGS="$CFLAGS -m486 -Di486"
      ;;
    i586)
      CFLAGS="$CFLAGS -mpentium -Di586"
      ;;
    i686)
      CFLAGS="$CFLAGS -mpentiumpro -Di686"
      ;;
    ia64)
      CFLAGS="$CFLAGS -Dia64"
      ;;
    powerpc)
      ;;
    *)
      AC_MSG_WARN([Don't know how to optimize for $target_cpu])
      ;;
    esac
  elif test "$ac_cv_prog_SUNPRO_CC" = yes; then
    CFLAGS="-fast -mt -KPIC"
    case $target_cpu in
    sparcv8plus)
      CFLAGS="$CFLAGS -Dsparcv8plus -xchip=ultra -xarch=v8plus"
      ;;
    sparcv9)
      CFLAGS="$CFLAGS -Dsparcv9 -xchip=ultra -xarch=v9"
      ;;
    *)
      AC_MSG_WARN([Using general optimization for $target_cpu])
      ;;
    esac
  else
    CFLAGS="-O2 -shared"
  fi
fi
case $target_os in
  freebsd*)
    CFLAGS="$CFLAGS -pthread"
    LDFLAGS="$LDFLAGS -pthread"
    ;;
esac

dnl Checks for assembler flags
case $target_cpu in
sparcv8plus)
  ASFLAGS="-Av8plus"
  ;;
sparcv9)
  ASFLAGS="-Av9"
  ;;
*)
  ASFLAGS=""
  ;;
esac

dnl Checks for devices
AC_MSG_CHECKING(for entropy device)
case $target_os in
  cygwin*)
    ;;
  freebsd*)
    AC_DEFINE(HAVE_DEV_RANDOM,1)
    AC_DEFINE(HAVE_DEV_URANDOM,1)
    AC_DEFINE(HAVE_DEV_TTY,1)
    ac_cv_entropy_device="random,urandom,tty"
    ;;
  linux*)
    AC_DEFINE(HAVE_DEV_DSP,1)
    AC_DEFINE(HAVE_DEV_RANDOM,1)
    AC_DEFINE(HAVE_DEV_URANDOM,1)
    AC_DEFINE(HAVE_DEV_TTY,1)
    ac_cv_entropy_device="dsp,random,urandom,tty"
    ;;
  solaris*)
    AC_DEFINE(HAVE_DEV_AUDIO,1)
    AC_DEFINE(HAVE_DEV_TTY,1)
    ac_cv_entropy_device="audio,tty"
    ;;
  *)
    AC_MSG_ERROR([
There is no entropy device to initialize the random generator on host $host
])
  ;;
esac
AC_MSG_RESULT($ac_cv_entropy_device)

dnl Generate the assembler optimizations
BLOWFISHOPTDEP=""
BLOWFISHMAKERULE=""
FIPS180OPTDEP=""
FIPS180OPTRULE=""
MP32OPTDEP=""
MP32OPTRULE=""
case $target_cpu in
  i[[3]]86)
    MP32OPTDEP="mp32opt.o: gnu/mp32opt.gas.i386.s"
    MP32OPTRULE="\$(COMPILE.s)"
    ;;
  i[[56]]86)
	BLOWFISHOPTDEP="blowfishopt.o: gnu/blowfishopt.gas.i586.s"
    BLOWFISHOPTRULE="\$(COMPILE.s)"
	FIPS180OPTDEP="fips180opt.o: gnu/fips180opt.gas.i586.s"
    FIPS180OPTRULE="\$(COMPILE.s)"
    MP32OPTDEP="mp32opt.o: gnu/mp32opt.gas.i386.s"
    MP32OPTRULE="\$(COMPILE.s)"
    ;;
  ia64)
    MP32OPTDEP="mp32opt.o: gnu/mp32opt.gas.ia64.s"
    MP32OPTRULE="\$(COMPILE.s)"
    ;;
  powerpc)
    MP32OPTDEP="mp32opt.o: gnu/mp32opt.gas.powerpc.s"
    MP32OPTRULE="\$(COMPILE.s)"
    ;;
  sparcv8plus)
    MP32OPTDEP="mp32opt.o: gnu/mp32opt.gas.sparcv9.s"
    MP32OPTRULE="\$(COMPILE.s)"
    ;;
  sparcv9)
    MP32OPTDEP="mp32opt.o: mp32opt.gas.sparcv9.s"
    MP32OPTRULE="\$(COMPILE.s)"
    ;;
esac
AC_SUBST(BLOWFISHOPTDEP)
AC_SUBST(BLOWFISHOPTRULE)
AC_SUBST(FIPS180OPTDEP)
AC_SUBST(FIPS180OPTRULE)
AC_SUBST(MP32OPTDEP)
AC_SUBST(MP32OPTRULE)

dnl Generate the config header
AC_CONFIG_HEADER(gnu/config.gnu.h)

dnl Output
AC_OUTPUT(Makefile)
